apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

def libVersionCode = 20000
def libraryVersion = '2.0.0-rc1'

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.3"

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion 25

		versionCode libVersionCode
		versionName libraryVersion

		// Set AndroidJUnitRunner as the default test instrumentation runner, to use JUnit 4 test classes
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	lintOptions {
		abortOnError false
	}
}

// The Dokka plugin generates the KDoc and Javadoc files
dokka {
	outputFormat = 'javadoc'
	outputDirectory = "$buildDir/docs/javadoc"
}

// Build KDocs after before build (if this is not run before the build then no javadoc files will be added to the
// javadoc.jar)
build.dependsOn(dokka)

// Disable javadoc tasks because they will override the files created by the dokka task
tasks.withType(Javadoc).all {
	enabled = false
}

dependencies {
	// Include Volley library from our Maven repository
	implementation 'com.android.volley:volley:1.0.0'

	// The Gson library is used to easily parse JSON objects
	implementation 'com.google.code.gson:gson:2.8.0'

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

	// Testing libs

	testImplementation 'junit:junit:4.12'
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

	androidTestImplementation 'com.android.support:support-annotations:25.4.0'

	androidTestImplementation 'com.android.support.test:runner:0.5'
	androidTestImplementation 'com.android.support.test:rules:0.5'

	androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}

// JCenter configuration and tasks

ext {
	bintrayOrg = 'hipsheep'
	bintrayRepo = 'maven'
	bintrayPackage = 'volley-wrapper'

	mavenGroupId = 'com.hipsheep'
	libName = 'volley-wrapper'
	libVersion = libraryVersion
	artifact = 'volley-wrapper'

	libDescription = 'A wrapper of the Volley network library for Android, which offers an easy interface to integrate and use.'

	siteUrl = 'https://github.com/hipsheep/volley-wrapper'
	gitUrl = 'https://github.com/hipsheep/volley-wrapper.git'

	developerName = 'Franco Sabadini'
	developerEmail = 'fsabadi@gmail.com'

	licenseName = 'The Apache Software License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	allLicenses = ["Apache-2.0"]
}

// Import Maven and Bintray code from external files
apply from: 'https://raw.githubusercontent.com/hipsheep/utils/master/gradle/maven.gradle'
apply from: 'https://raw.githubusercontent.com/hipsheep/utils/master/gradle/bintray.gradle'